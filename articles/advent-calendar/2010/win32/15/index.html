<!doctype html>
<html lang="ja">
    <head>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
        <meta name="description" content="">
        <meta name="author" content="turugina <turugina@cpan.org>">
        <title>Eject your optical drives! - Articles Advent Calendar 2010 Win32</title>
        <link rel="icon" href="/img/favicon.ico">
        <link href="TODO" rel="stylesheet">
    </head>
    <body>
        <h1>Eject your optical drives!</h1>
        tag

        <p>こんにちはこんにちは、1日仕事を休んだらすっかり土曜日気分になってしまった<a href="http://twitter.com/turugina">turugina</a>です。</p><p>さて、今回は、Windowsにおいて、記憶デバイス(HDDやCDROM等)の制御を行ってくれるAPIのwrapperを提供してくれる<a href="http://search.cpan.org/dist/Win32API-File">Win32API::File</a>を使ったサンプルを紹介したいと思います。</p><p>というわけで、いきなりコードですが</p>

    <pre class="code prettyprint lang-perl"># eject.pl
use strict;
use warnings;

use v5.10.0;

use Win32API::File qw ( :ALL );

my @drives = do {
  if ( @ARGV ) {
    # ドライブが引数で指定されていればそれを使う
    map uc, grep /^[abcdefghijklmnopqrstuvwxyz]:$/i, @ARGV;
  }
  else {
    # 指定されていなければ有効なドライブを全て列挙する。
    my $buf = &#39; &#39; x (4*26+1);
    GetLogicalDriveStrings(length($buf), $buf);

    # $bufの中身は &#34;[ドライブレター]:\\\0[ドライブレター]:\\\0...&#34;
    split /\\\0/, $buf;
  }
};

# 光学ドライブだけ取り出す
my @cddrives = grep { GetDriveType($_) == DRIVE_CDROM } @drives;
if ( !@cddrives ) {
  say &#34;no CD/DVD drives!&#34;;
  exit 0;
}

for my $d ( @cddrives ) {
  # ドライブのハンドルを取得
  my $h = createFile(&#34;//./$d&#34;, &#39;r&#39;, &#39;r&#39;);
  if ( !$h ) {
    say $^E, &#34; [$d]&#34;;
    next;
  }

  # イジェクト！
  if ( DeviceIoControl( $h, IOCTL_STORAGE_EJECT_MEDIA, [], [], [], [], [], [] ) ) {
    say &#34;EJECT! [$d]&#34;;
  }
  else {
    say $^E, &#34; [$d]&#34;;
  }
}</pre>
<p>こんな感じで、引数に何も指定せずに実行すると全ての光学ドライブを、引数でドライブを指定するとそれが光学ドライブであればイジェクトしてまわります。</p><p>このスクリプトの使い道ですが、日常使いの他、ちょっとコードを書き足して<a href="http://0xcc.net/cdbiff/">メールを受信したらトレイを排出する</a>とかTwitterでmentions_timelineやdirect messagesに新しいメッセージが来たらトレイを排出するとか、トレイの前にピタゴラ装置的な物をこしらえて楽しむとか、色々考えられますね！</p><p>ちなみに、Unix系OSにはejectコマンドというものがあって、これで光学ドライブやフロッピーなどのイジェクトを行えるようになっています。もし↑のスクリプトで光学ドライブ以外にイジェクトしたいものがあれば、<a href="http://msdn.microsoft.com/ja-jp/library/cc429310.aspx">GetDriveType API</a>の戻り値チェックでDRIVE_REMOVABLEも候補に入れてあげれば良いでしょう。(多分(ぇ</p><br />
<p><hr /></p><p>と、ここまで書いてきましたが、実は上記のスクリプト相当のものは<a href="http://search.cpan.org/dist/SDL/">SDL</a>というモジュールをインストールすれば、</p>

    <pre class="code prettyprint lang-perl">use strict;
use warnings;

use SDL;
use SDL::CDROM;
use SDL::CD;

SDL::init(SDL_INIT_CDROM);

my @cddrives = 0 .. SDL::CDROM::num_drives() - 1;
die &#34;no available drives&#34; if !@cddrives;

if ( @ARGV ) {
  my %m = map { SDL::CDROM::name($_) =&gt; $_ } @cddrives;
  @cddrives = grep defined,@m{@ARGV};
  die &#39;no available drives: choose &#39;, join &#39;, &#39;,grep defined $m{$_},keys %m if !@cddrives;
}

SDL::CD-&gt;new($_)-&gt;eject for @cddrives</pre>
<p>という風にとても短く書けます。 しかもWindows以外の環境でも動作するという優れもの！(白目</p><br />
<p>さて、綺麗に(?)オチがついたところで今回はこの辺で...<br />
次はどなたでしょうかー。</p>

        <script src="https://cdn.jsdelivr.net/gh/google/code-prettify@master/loader/run_prettify.js?lang=css&amp;skin=sunburst"></script>
    </body>
</html>
