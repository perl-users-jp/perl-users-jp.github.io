<!doctype html>
<html lang="ja">
    <head>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, initial-scale=1, shrink-to-fit=no">
        <meta name="description" content="">
        <meta name="author" content="xaicron <xaicron@cpan.org>">
        <title>Win32::GUITest と Win32::Clipboard で GUI 操作を自動化する - Articles Advent Calendar 2010 Win32</title>
        <link rel="icon" href="/img/favicon.ico">
        <link href="TODO" rel="stylesheet">
    </head>
    <body>
        <h1>Win32::GUITest と Win32::Clipboard で GUI 操作を自動化する</h1>

        <p>こんにちわ。のどの痛みと熱でだいぶ体がだるい上に、開けていないフィギュアの箱を思いっきり踏んでしまって軽く凹んでいる <a href="http://blog.livedoor.jp/xaicron/">xaicron</a> です。</p><p>今日紹介するモジュールは <a href="http://search.cpan.org/dist/Win32-GuiTest/">Win32::GUITest</a> というとってもナイスなモジュールです。<br />
これの詳しい説明は、その昔に <a href="/articles/advent-calendar/2008/13.html">amachang が書いています</a>のでそちらを見るのが良いかと思います。<br />
簡単にいうと、window 名から window を取得したり、キーボード入力やマウス入力をエミュレートしてくれる便利なやつです。</p><p>普段は、iTunes や foobar2000 を愛用していますが、たまに楽曲情報が見つからなかったりするときに、割と簡単に gracenote に情報を登録できるので SonicStage を使ってます。（最近、X-Application とかいう名前に変わってしまったみたいですが）<br />
しかし、これのインターフェースってまぁ当然のことながら GUI なんですよね。で、GUI ってめんどくさいじゃないですか（ぇ<br />
というわけで、YAML ファイルからタイトルや楽曲名を取ってきて、この GUI にテロテロと突っ込んでくれるやつを試しに書いてみました。</p>

    <pre class="code lang-perl">use strict;
use warnings;
use utf8;
use Win32::GuiTest &#39;:ALL&#39;;
use Win32::Clipboard;
use Win32::API;
use YAML 0.71 qw/LoadFile/;
use Data::Recursive::Encode;
use Time::HiRes qw/sleep/;

my $file = shift || die &#34;Usage: $0 config.yaml&#34;;
my $data = Data::Recursive::Encode-&gt;encode(cp932 =&gt; LoadFile $file);

UnicodeSemantics 1;

my $clip = Win32::Clipboard();
$clip-&gt;Empty;

my ($win, @wins) = FindWindowLike 0 =&gt; &#39;^Gracenote&#39;;
die &#39;Oops!&#39; if @wins;
my $childs = [ GetChildWindows $win ];
SetForegroundWindow $win;

my $MessageBox = Win32::API-&gt;new(&#39;user32&#39;, &#39;MessageBoxA&#39;, &#39;NPPN&#39;, &#39;N&#39;);

&amp;main();exit;

sub main {
    set_data_lang();
    set_title($data-&gt;{title});
    set_artist($data-&gt;{artist});

    $MessageBox-&gt;Call(0, Encode::encode(cp932 =&gt; &#39;ジャンルを選択して、このダイアログを閉じてください&#39;), &#39;perl&#39;, 0x00000030 | 0x00010000 | 0x00040000);
    SetForegroundWindow $win;

    select_track_tab();
    set_tracks($data-&gt;{tracks});
}

# Album Tab
sub set_data_lang {
    my $lang_combo = get_next_component(&#39;^データ言語$&#39;);
    SetFocus $lang_combo;
    SelComboItem $lang_combo, 43; # 日本語
}

sub set_title {
    my $title = shift;
    my $title_edit = get_next_component(&#39;^アルバム情報$&#39;);
    SetFocus $title_edit;
    sleep 0.1;
    SendKeys &#39;{END}&#39;;
    SendKeys &#39;{BS}&#39; for (0..8);
    $clip-&gt;Set($title);
    SendKeys &#39;^v&#39;;
}

sub set_artist {
    my $artist = shift;
    my $artist_edit = get_next_component(&#39;^レコーディング アーティスト$&#39;);
    SetFocus $artist_edit;
    sleep 0.1;
    $clip-&gt;Set($artist);
    SendKeys &#39;^v&#39;;
}

# Track Tab
sub select_track_tab {
    my ($tab1) = FindWindowLike $win, &#39;^Tab1$&#39; or die &#39;Oops&#39;;
    SetFocus $tab1;
    sleep 0.1;
    SendKeys &#39;{RIGHT}&#39;;
}

sub set_tracks {
    my $tracks = shift;
    my ($track_list, $i) = get_next_component(&#39;^トラック 1/\d+$&#39;);
    my $track_edit = $childs-&gt;[$i + 2];
    for my $track (@$tracks) {
        SetFocus $track_edit;
        sleep 0.1;
        SendKeys &#39;{END}&#39;;
        SendKeys &#39;{BS}&#39; for (0..9);
        $clip-&gt;Set($track);
        SendKeys &#39;^v&#39;;
        SendKeys &#39;+{TAB}&#39;;
        SendKeys &#39;{DOWN}&#39;;
    }
}

# Util
sub get_next_component {
    my $reg = shift;

    my $i = 0;
    for (; $i &lt; @$childs; $i++) {
        last if GetWindowText($childs-&gt;[$i]) =~ /$reg/;
    }

    return $childs, $i + 1 if wantarray;
    return $childs-&gt;[$i + 1];
}</pre>
<p>ソースはこんなんで、</p>

    <pre class="code lang-yaml">---
title: Last Song
artist: Girls Dead Monster starring marina
tracks:
  - Last Song
  - Hot Meal
  - God Bless You</pre>
<p>みたいな YAML を食わせます。</p><p>これを X-Application を起動して、以下の画面の時に実行します。<br />
<img width="420px" src="https://github.com/perl-users-jp/perl-advent-calendar/raw/master/2010/win32/2010-12-08/dialog1.png" /></p>

    <pre class="code">$ insert_gracenote.pl track_list.yaml</pre>
<p>そうすると、タイトルとアーティストが入力されて、以下のようにダイアログが出るので、適当にジャンルを選んであげます。<br />
<img width="420px" src="https://github.com/perl-users-jp/perl-advent-calendar/raw/master/2010/win32/2010-12-08/dialog2.png" /></p><p>選び終わってダイアログを閉じると、トラックリストの入力になるので、あとは入力が終わるまで待てばOKです！<br />
最終的に以下のような感じになってめでたしめでたし。<br />
<img width="420px" src="https://github.com/perl-users-jp/perl-advent-calendar/raw/master/2010/win32/2010-12-08/dialog3.png" /></p><p>Win32::GUITest には WMSetText() という、edit box に文字を突っ込む API があるんですが、なんかこいつだとうまくいかなかったので、頑張って バックスぺースで文字を消したり、YAML の定義をクリップボードに突っ込んでそれを Ctrl-V で貼りつけたりとかなりナイーブな実装になったけど、なんとなく動きましたね！！</p><p>Win32::GUITest + Win32::Clipboard を使えば、GUI 操作もお手の物ですね！！バンザイ！！</p>

        <script src="https://cdn.jsdelivr.net/gh/google/code-prettify@master/loader/run_prettify.js?lang=css&amp;skin=sunburst"></script>
    </body>
</html>
